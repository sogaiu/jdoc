#! /usr/bin/env janet

(def usage
  ``
  Usage: jdoc [option] | jdoc [identifier]

  View Janet docstrings from the command line.

    -h, --help                   show this output

    --bash-completion            output bash-completion bits
    --fish-completion            output fish-completion bits
    --zsh-completion             output zsh-completion bits
    --raw-all                    show all names for completion

  With a full identifier, but no options, show docstring for
  identifier.

  With a partial identifier, but no options, show candidates
  with docstrings.

  Without an identifier, show docstring for some identifier.
  ``)

########################################################################

(def bash-completion
  ``
  _jdoc_ids() {
      COMPREPLY=( $(compgen -W "$(jdoc --raw-all)" -- ${COMP_WORDS[COMP_CWORD]}) );
  }
  complete -F _jdoc_ids jdoc
  ``)

(def fish-completion
  ``
  function __jdoc_complete_ids
    if not test "$__jdoc_ids"
      set -g __jdoc_ids (jdoc --raw-all)
    end

    printf "%s\n" $__jdoc_ids
  end

  complete -c jdoc -a "(__jdoc_complete_ids)" -d 'ids'
  ``)

(def zsh-completion
  ``
  #compdef jdoc

  _jdoc_ids() {
      local matches=(`jdoc --raw-all`)
      compadd -a matches
  }
  _jdoc "$@"
  ``)

(defn escape
  [name]
  # XXX: doing multiple linear searches seems bad
  (cond
    (string/find "*" name)
    (string "\"" name "\"")
    #
    (string/find "<" name)
    (string "\"" name "\"")
    #
    (string/find ">" name)
    (string "\"" name "\"")
    # XXX: any other characters to be careful of?
    name))

########################################################################

(defn main
  [& argv]
  (def arg (get argv 1))

  # option handling, except --raw-all
  (when (and arg
             (string/has-prefix? "-" arg)
             (not= "--raw-all" arg))
    (cond
      (or (= "--help" arg) (= "-h" arg))
      (print usage)
      #
      (= "--bash-completion" arg)
      (print bash-completion)
      #
      (= "--fish-completion" arg)
      (print fish-completion)
      #
      (= "--zsh-completion" arg)
      (print zsh-completion)
      #
      (do
        (eprintf "unrecognized option: %s" arg)
        (eprint "Try jdoc -h for usage.")
        (os/exit 1)))
    (os/exit 0))

  (when (= "--raw-all" arg)
    (each binding (all-bindings)
      (print (escape binding)))
    (os/exit 0))

  (def doc-arg
    (if arg
      (if (get (curenv) (symbol arg))
        arg
        (string/format `"%s"` arg))
      (do
        (def bz (all-bindings))
        (def idx (math/rng-int (math/rng (os/cryptorand 3))
                               (length bz)))
        (get bz idx))))

  (eval-string (string/format "(doc %s)" doc-arg)))

## future possibilities ##

# * docstrings from within "current" project

